plugins {
    id 'com.google.protobuf' version '0.9.4'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.flywaydb.flyway' version '8.2.0'
    id 'java'
}


def envFile = file(".env")
if (envFile.exists()) {
    println "Loading environment variables from .env file"
    envFile.eachLine { line ->
        def parts = line.split('=', 2)
        if (parts.length == 2) {
            def key = parts[0].trim()
            def value = parts[1].trim()
            project.ext.set(key, value)
        }
    }
}

group = 'com.rus1it'
version = '0.0.1-SNAPSHOT'
description = 'user-service-cashpilot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
    grpcVersion = '1.63.0'
    protobufVersion = '3.25.3'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.cloud:spring-cloud-dependencies:2023.0.2'
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
    implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
    implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.mapstruct:mapstruct:1.6.2'
    implementation 'build.buf.protoc-gen-validate:pgv-java-stub:0.8.0'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
    protobuf 'com.google.api.grpc:proto-google-common-protos:2.27.0'
    compileOnly 'org.projectlombok:lombok'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        validate {
            artifact = "build.buf.protoc-gen-validate:protoc-gen-validate:0.8.0"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
                validate {
                    option 'lang=java'
                }
            }

            task.builtins {
                java {}
            }

            task.includeDirs.from("${projectDir}/src/main/proto")
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}